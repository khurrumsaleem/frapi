MODULE PlotFile
    USE Kinds
    USE conversions_frapcon
    USE variables_frapcon
    USE Gas, ONLY : ngases
    USE RunProperties
    IMPLICIT NONE
    !>@brief
    !> This module contains the output data for the plot file. The plot file can be read by
    !> the FRAPPlot (Excel program) or APT Plot.
    !>@author
    !> Ian Porter, NRC
    !>@date
    !> 5/20/2015
    !
    CONTAINS
    !
    SUBROUTINE grafini ()
    USE Kinds
    USE conversions_frapcon
    USE Refabrication, ONLY : irefab, nrefab1, nrefab2
    IMPLICIT NONE
    !>@brief
    !> This subroutine writes the header information for the plot file
    !
    INTEGER(ipk) :: i
    LOGICAL :: unit
    CHARACTER(LEN=8) :: ctime
    CHARACTER(LEN=9) :: day
    !
    CALL edate(day)
    CALL clockx(ctime)
    !
    IF (nunits == 0) THEN
        unit = .FALSE.
    ELSE
        unit = .TRUE.
    END IF
    !
    WRITE (punit,101) TRIM(codeid), TRIM(buildid)
    WRITE (punit,500) day, ctime, TRIM(title)
101 FORMAT(' strip file generated by:',/,5x,a,/,2x,a)
500 FORMAT('     Current date: ',a9,'  Time: ',a8/,'     For Case: ',a/)
    WRITE (punit,*)'data'
    WRITE (punit,*)'1 days days Time'
    WRITE (punit,*)'2 kW/ft kW/m Average Power'
    WRITE (punit,*)'20 kW/ft kW/m Peak Power'
    WRITE (punit,*)'10 MWd/kgU MWd/kgU Rod Average Burnup'
    WRITE (punit,*)'35 MWd/kgU MWd/kgU Rod Peak Power Burnup'
    WRITE (punit,*)'34 na na Peak Power Axial Element Indicator'
    WRITE (punit,*)'36 F K Peak Power Pellet CenterLine Temperature'
    WRITE (punit,*)'37 F K Peak Power Pellet Surface Temperature'
    WRITE (punit,*)'38 F K Peak Power Clad Inside Temperature'
    WRITE (punit,*)'39 F K Peak Power Clad Outside Temperature'
    WRITE (punit,*)'8 F K Average Fuel Temperature'
    WRITE (punit,*)'9 Btu/lbm j/kg Fuelled Region Stored Energy'
    WRITE (punit,*)'5 F K Plenum Gas Temperature'
    WRITE (punit,*)'6 psia mpa Plenum Pressure'
    WRITE (punit,*)'7 in^3 cm^3 Total Void Volume'
    WRITE (punit,*)'3 in mm Fuel Stack Axial Extension'
    WRITE (punit,*)'4 in mm Cladding Axial Extension (Including Irrad Growth)'
    WRITE (punit,*)'33 % % Relative Cladding Axial Extension'
    WRITE (punit,*)'40 % % Relative Fuel Axial Extension'
    WRITE (punit,*)'11 % % Fission Gas Release'
    WRITE (punit,*)'21 % % Molar Fraction of He'
    WRITE (punit,*)'22 % % Molar Fraction of Fission Products (Xe+Kr)'
    WRITE (punit,*)'23 % % Molar Fraction of Air'
    WRITE (punit,*)'24 % % Molar Fraction of Ar'
    WRITE (punit,*)'25 na na Free Gas in the Rod cm3STP'
    WRITE (punit,*)'26 na na Moles of Fission Gas Released'
    IF (ifba > 0.0_r8k .AND. b10 > 0.0_r8k) WRITE (punit,*)'27 na na Moles of He Released from ZrB2 liner'
    WRITE (punit,*)'28 na na Moles of Gas in Rod'
    WRITE (punit,*)'1D data'
    WRITE (punit,*)'130 kW/ft kW/m Axial Power'
    WRITE (punit,*)'156 na na Normalized axial node power'
    WRITE (punit,*)'116 Btu/(hr-ft^2) W/(m^2) Surface Heat Flux'
    WRITE (punit,*)'142 GWd/MTU GWd/MTU Nodal Burnup'
    WRITE (punit,*)'157 n/m^2 n/m^2 Axial Fast Fluence'
    WRITE (punit,*)'150 ft m Axial Element Elevation'
    WRITE (punit,*)'122 F K Fuel Centerline Temperature'
    WRITE (punit,*)'123 F K Fuel Pellet Surface Temperature'
    WRITE (punit,*)'145 F K Fuel Volume Average Temperature'
    WRITE (punit,*)'158 F K Gap Average Temperature'
    WRITE (punit,*)'124 F K Cladding Inside Temperature'
    WRITE (punit,*)'125 F K Cladding Average Temperature'
    WRITE (punit,*)'126 F K Cladding Outside Temperature'
    WRITE (punit,*)'139 F K Oxide Surface Temperature'
    WRITE (punit,*)'127 F K Bulk Coolant Temperature'
    WRITE (punit,*)'114 na na Fuel Duty Index'
    WRITE (punit,*)'121 Btu/lbm j/kg Stored Energy'
    WRITE (punit,*)'107 psia mpa Cladding Axial Stress'
    WRITE (punit,*)'108 psia mpa Cladding Hoop Stress'
    WRITE (punit,*)'109 psia mpa Effective Cladding Stress'
    WRITE (punit,*)'101 na na Cladding Axial Strain'
    WRITE (punit,*)'102 na na Cladding Hoop Strain'
    WRITE (punit,*)'103 na na Cladding Radial Strain'
    WRITE (punit,*)'136 na na Cladding Elastic Hoop Strain'
    WRITE (punit,*)'137 na na Cladding Elastic Axial Strain'
    WRITE (punit,*)'138 na na Cladding Elastic Radial Strain'
    WRITE (punit,*)'104 na na Cladding Perm Axial Strain'
    WRITE (punit,*)'105 na na Cladding Perm Hoop Strain'
    WRITE (punit,*)'106 na na Cladding Perm Radial Strain'
    WRITE (punit,*)'144 in/in/s m/m/s Cladding Creep Rate'
    WRITE (punit,*)'152 % % Axial Strain due to Irradiation growth'
    WRITE (punit,*)'169 mils microns Cladding Inner Radius Total Deformation'
    WRITE (punit,*)'170 mils microns Cladding Outer Radius Total Deformation'
    WRITE (punit,*)'128 mils microns Fuel Surface Displacement'
    WRITE (punit,*)'161 mils microns Fuel Thermal Expansion'
    WRITE (punit,*)'162 mils microns Fuel Swelling'
    WRITE (punit,*)'164 mils microns Fuel Densification'
    WRITE (punit,*)'165 mils microns Fuel Relocation'
    WRITE (punit,*)'143 in/in/s m/m/s Fuel Swelling Rate'
    WRITE (punit,*)'133 na na Fuel Surface Axial Strain'
    WRITE (punit,*)'141 % % Nodal FGR'
    IF (ifba > 0.0_r8k .AND. b10 > 0.0_r8k) WRITE (punit,*)'148 % % B-10 Enrichment Remaining'
    WRITE (punit,*)'131 psia mpa Gap Interface Pressure'
    WRITE (punit,*)'129 psia mpa Gap Gas Pressure'
    WRITE (punit,*)'155 mils microns Mechanical Gap'
    WRITE (punit,*)'112 mils microns Thermal Gap'
    WRITE (punit,*)'146 BTU/(hr-ft^2-F) W/(m^2K) Gap Conductance'
    SELECT CASE (icm)
    CASE (2)
        WRITE (punit,*)'115 mils microns Zircaloy-2 Oxide Thickness'
        WRITE (punit,*)'140 ppm ppm Zircaloy-2 Hydrogen Concentration'
    CASE (4)
        WRITE (punit,*)'115 mils microns Zircaloy-4 Oxide Thickness'
        WRITE (punit,*)'140 ppm ppm Zircaloy-4 Hydrogen Concentration'
    CASE (5)
        WRITE (punit,*)'115 mils microns M5 Oxide Thickness'
        WRITE (punit,*)'140 ppm ppm M5 Hydrogen Concentration'
    CASE (6)
        WRITE (punit,*)'115 mils microns ZIRLO(TM) Oxide Thickness'
        WRITE (punit,*)'140 ppm ppm ZIRLO(TM) Hydrogen Concentration'
    CASE (7)
        WRITE (punit,*)'115 mils microns Optimized ZIRLO Oxide Thickness'
        WRITE (punit,*)'140 ppm ppm Optimized ZIRLO Hydrogen Concentration'
    END SELECT
    WRITE (punit,*)'117 lbm/ft^3 kg/m^3 Coolant Density'
    WRITE (punit,*)'118 lb/(hr-ft^2) kg/(s-m^2) Coolant Mass Flux'
    WRITE (punit,*)'119 psia mpa Coolant Pressure'
    IF (nplot > 1 .AND. ngasmod == 3) THEN
        WRITE (punit,*) '180 im mm Fuel rim length'
    ENDIF
    WRITE (punit,*)'2D data'
    WRITE (punit,*)'201 F K Fuel Rod Mesh Temperatures'
    IF (nplot > 1) THEN
        WRITE (punit,*)'202 in mm Fuel Rod Mesh Radial Nodes'
        WRITE (punit,*)'203 MWd/kgU MWd/kgU Fuel Rod Mesh Burn Up'
        WRITE (punit,*)'204 na na Linear Porosity Fuel Fraction'
        WRITE (punit,*)'205 na na Pu Concentration'
        WRITE (punit,*)'206 na na Radial Power Profile'
        WRITE (punit,*)'207 g/cm^3 g/cm^3 Radial Fuel Density'
    END IF
    IF ((nplot > 1) .AND. ((ngasmod == 2) .OR. (ngasmod == 3) .OR. (ngasmod == 4))) THEN
        WRITE (punit,*)'gas data'
        WRITE (punit,*)'501 in mm Gas Mesh Radial Nodes'
        WRITE (punit,*)'502 cm3STP/g cm3STP/g Gas Mesh Produced Fission Gas Concentration'
        WRITE (punit,*)'503 cm3STP/g cm3STP/g Gas Mesh Released Fission Gas Concentration'
        WRITE (punit,*)'504 cm3STP/g cm3STP/g Gas Mesh Retained Fission Gas Concentration'
        WRITE (punit,*)'505 cm3STP/g cm3STP/g Gas Mesh Grain Boundaries Gas Concentration'
        WRITE (punit,*)'506 cm3STP/g cm3STP/g Gas Mesh Gas Within Grains Concentration'
        WRITE (punit,*)'507 cm3STP/g cm3STP/g Gas Mesh Re-Solved Gas Concentration'
        WRITE (punit,*)'520 na na Fuel restructuring'
        WRITE (punit,*)'521 % % Fuel porosity'
    END IF
    WRITE (punit,*)'end data'
    ! Write the number of axial and radial nodes to plot file as well as the units indicator to select the output units
    WRITE (punit,*) 'naxn= ', na - 1
    WRITE (punit,*) 'nmesh= ', nr + 3
    WRITE (punit,*) 'unit= ', unit
    WRITE (punit,*) 'nrfuel= ', nr
    WRITE (punit,*) 'nrclad= ', 2
    WRITE (punit,*) 'nroxyd= ', 1
    WRITE (punit,*) 'nrgas= ', ngasr
    WRITE (punit,*) 'nsteps= ', im - 1
    WRITE (punit,*) 'irefab= ', irefab
    WRITE (punit,*) 'nrefab1= ', nrefab1
    WRITE (punit,*) 'nrefab2= ', nrefab2
    SELECT CASE (ngasmod)
        CASE (1)
            WRITE (punit,*) 'ngasmod= ANS5.4'
        CASE (2)
            WRITE (punit,*) 'ngasmod= Massih'
        CASE (3)
            WRITE (punit,*) 'ngasmod= FRAPFGR'
        CASE (4)
            WRITE (punit,*) 'ngasmod= ANS5.4_2011'
    END SELECT
    SELECT CASE (mechan)
        CASE (1)
            WRITE (punit,*) 'mechan= FEA'
        CASE (2)
            WRITE (punit,*) 'mechan= FRACAS-I'
    END SELECT
    !
    END SUBROUTINE grafini
    !
    !
    !
    SUBROUTINE grafout
    USE Kinds
    USE conversions_frapcon
    USE FissionGas, ONLY : FGRData
    USE Comde
    IMPLICIT NONE
    !>@brief
    !> This subroutine prints the data to the plot file at every timestep
    !
    INTEGER(ipk) :: i, l, z, lg, i1
    REAL(r8k) :: aupk, FuelledLength, TempSum, EnergySum, xc, zn, xcc, eps3, rcih, rcoh, &
      &          rox, punba, unba, xrhof, rgas, radin, radou, oeps
    LOGICAL :: unit
    !
    REAL(r8k), DIMENSION(na) :: xrhouo2
    REAL(r8k), DIMENSION(nr) :: csfrad             ! Cold state fuel radii's from center to outside
    REAL(r8k), DIMENSION(:,:), ALLOCATABLE :: edat ! Variable written to plot file
    REAL(r8k), DIMENSION(nr,na-1) :: hsfrad        ! Hot state fuel radii's from center to outside
    REAL(r8k), DIMENSION(ngasr+1,na-1) :: gasrad
    REAL(r8k), DIMENSION(nr,na-1) :: epsi
    !
    ! Allocate scratch variable based on largest array size
    i1 = MAX(nr + 3, ngasr)
    IF (.NOT. ALLOCATED(edat)) ALLOCATE (edat(1:i1,1:na))
    edat = 0.0_r8k
    l = 1
    !
    !***** Determine the calculated variables.
    !
    FuelledLength = 0.0_r8k
    TempSum = 0.0_r8k
    EnergySum = 0.0_r8k
    DO z = 1, (na - 1)
        TempSum =  TempSum + PelAveTemp(z) * deltaz(z)
        EnergySum = EnergySum + StoredEnergy(z) * deltaz(z)
        FuelledLength = FuelledLength + deltaz(z)
    END DO
    !
    !      nunits = 0
    !
    unit = .TRUE.
    IF (nunits == 0) unit = .FALSE.
    !
    !***** Output the non-nodally dependant variables.
    !
    !
    !***** Output the ProblemTime (days)
    !
    WRITE (punit,*) "===next time step ",it - 1
    WRITE (punit,*) "1 ", ProblemTime(it) * sectoday
    !
    !***** Output the average fuel rod power.
    !
    aupk = qmpy(it) * BTUhtokW * (dcoBOL * intoft * pi) / fa
    IF (.NOT. unit) aupk = aupk * kWfttokWm
    WRITE (punit,*) "2 ", aupk
    !
    !***** Output the peak linear heat rating.
    !
    aupk = qpeak
    IF (.NOT. unit) aupk = aupk * kWfttokWm
    WRITE (punit,*) "20 ", aupk
    !
    !***** Output rod average burnup
    !
    aupk = bu / 1000.0_r8k
    WRITE (punit,*) "10 ", aupk
    !
    !***** Output the Peak burn up.
    !
    aupk = pkBurnup(it-1) / 1000.0_r8k
    WRITE (punit,*) "35 ", aupk
    !
    !***** Output the Axial Max Flux Element Indicator.
    !
    aupk = jpeak(1) - 1
    WRITE (punit,*) "34 ", jpeak(1) - 1
    !
    !***** Output the Peak Pellet CenterLine Temperature.
    !
    aupk = pkPelCentTemp(it-1)
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "36 ", aupk
    !
    !***** Output the Peak Pellet Surface Temperature.
    !
    aupk = pkPelSurfTemp(it-1)
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "37 ", aupk
    !
    !***** Output the Peak Clad Inside Temperature.
    !
    aupk = pkIDCladTemp(it-1)
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "38 ", aupk
    !
    !***** Output the Peak Clad Outside Temperature.
    !
    aupk = pkODCladTemp(it-1)
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "39 ", aupk
    !
    !***** Output the average fuel temperature.
    !
    aupk = TempSum / FuelledLength
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "8 ", aupk
    !
    !***** Output the fuel rod stored energy
    !
    aupk = EnergySum / FuelledLength
    IF (.NOT. unit) aupk = aupk * BTUlbtoJkg
    WRITE (punit,*) "9 ", aupk
    !
    !***** Output the plenum gas temp.
    !
    aupk = tplen
    IF (.NOT. unit) aupk = tfk(aupk)
    WRITE (punit,*) "5 ", aupk
    !
    !***** Output the plenum gas pressure.
    !
    aupk = press
    IF (.NOT. unit) aupk = aupk * PSItoMPa
    WRITE (punit,*) "6 ", aupk
    !
    !***** Output the total free gas volume.
    !
    aupk = thvv
    IF (.NOT. unit) aupk = aupk * in3tocm3
    WRITE (punit,*) "7 ", aupk
    !
    !***** Output the fuel stack axial ext.
    !
    aupk = dhfll
    IF (.NOT. unit) aupk = aupk * intomm
    WRITE (punit,*) "3 ", aupk
    !
    !***** Output the cladding axial ext.
    !
    aupk = dlrod
    IF (.NOT. unit) aupk = aupk * intomm
    WRITE (punit,*) "4 ", aupk
    !
    !***** Output the relative cladding axial ext.
    !
    aupk = (dlrod * intoft) / totl * 100.0_r8k
    WRITE (punit,*) "33 ", aupk
    !
    !***** Output the relative fuel axial ext. (%)
    !
    aupk = hfper
    WRITE (punit,*) "40 ", aupk
    !
    !***** Output fission gas release
    !
    SELECT CASE (ngasmod)
    CASE (1, 2, 3)
        aupk = tfgfr * 100.0_r8k
    CASE (4)
        aupk = SUM(RB_rod(:,it)) * 100.0_r8k
    END SELECT
    WRITE (punit,*) "11 ", aupk
    !
    !***** Output molar fraction of He (Scanair output).
    !
    aupk = amfhe
    WRITE (punit,*) "21 ", aupk
    !
    !***** Output molar fraction of Fission Products (Xe+Kr) (Scanair output).
    !
    aupk = amfxe + amfkry
    WRITE (punit,*) "22 ", aupk
    !
    !***** Output molar fraction of Air (Scanair output).
    !
    aupk = amfair
    WRITE (punit,*) "23 ", aupk
    !
    !***** Output molar fraction of Ar (Scanair output).
    !
    aupk = amfarg
    WRITE (punit,*) "24 ", aupk
    !
    !***** Output Free gas in the rod cm3STP (Scanair output).
    ! Note: 1 mol of ideal gas at STP occupies 22.4 Liters
    aupk = gasmo(it-1) * 22.4_r8k * 1000.0_r8k
    WRITE (punit,*) "25 ", aupk
    !
    !***** Output Moles of Fission Gas Released
    !
    aupk = gmlesReleased - He_IFBA(it)
    WRITE (punit,*) "26 ", aupk
    !
    !***** Output Moles of He release from ZrB2 liner
    !
    IF (ifba > 0.0_r8k .AND. b10 > 0.0_r8k) THEN
        aupk = He_IFBA(it)
        WRITE (punit,*) "27 ", aupk
    END IF
    !
    !***** Output total moles of gas in rod
    !
    aupk = gasmo(it-1)
    WRITE (punit,*) "28 ", aupk
    !
    ! 1D Data
    !
    !
    !***** Output the Axial Power.
    !
    DO z = 1, (na - 1)
        edat(1,z) = Power(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * kWfttokWm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("130 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Normalized Axial Node Power.
    !
    DO z = 1, (na  - 1) 
        edat(1,z) = qaxnorm(z ,m)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("156 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Surface Heat Flux.
    !
    DO z = 1, (na - 1)
        edat(1,z) = qc(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * Bhft2toWm2
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("116 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the nodal Burnup.
    !
    DO z = 1, (na - 1)
        edat(1,z) = EOSNodeburnup(z) / 1000.0_r8k
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("142 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fluence.
    !
    DO z = 1, (na  - 1)
        edat(1,z) = FastFluence(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("157 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the axial elevation.
    !
    xc = 0.0_r8k
    DO z = 1, (na - 1)
        IF (z == 1) THEN
            xc = deltaz(1) / 2.0_r8k
        ELSE
            xc = xc + (deltaz(z-1) + deltaz(z)) / 2.0_r8k
        END IF
        edat(1,z) = xc * (1.0_r8k + eps(z,2))
        IF (.NOT. unit) edat(1,z) = edat(1,z) * ftom
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("150 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Centerline Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = tmpfuel(nr,z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("122 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fuel Pellet Surface Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = tmpfuel(1,z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("123 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fuel Volume Average Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = PelAveTemp(z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("145 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Gap Average Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = GapAveTemp(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("158 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding inside surface temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = CladInSurfTemp(z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("124 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Average Cladding Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = CladAveTemp(z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("125 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding outer temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = CladOutSurfTemp(z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("126 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the oxide surface temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = SurfTempOxide(z)
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("139 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Bulk Coolant Temperature.
    !
    DO z = 1, (na - 1)
        edat(1,z) = (BulkCoolantTemp(z) + BulkCoolantTemp(z+1)) / 2.0_r8k
        IF (.NOT. unit) edat(1,z) = tfk(edat(1,z))
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("127 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fuel Duty Index
    !
    DO z = 1, (na - 1)
        edat(1,z)= (MAX(0.0_r8k, (FDItave(z) - 580.0_r8k) / 100.0_r8k * ProblemTime(it) * sectohr / 1000.0_r8k)) ** 2
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("114 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fuel Stored Energy
    !
    DO z = 1, (na - 1)
        edat(1,z) = StoredEnergy(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * BTUlbtoJkg
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("121 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding axial stress.
    !
    DO z = 1, (na - 1)
        edat(1,z) = sig(z,2)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("107 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding hoop stress.
    !
    DO z = 1, (na - 1)
        edat(1,z) = sig(z,1)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("108 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Effective Clad Stress.
    !
    DO z = 1, (na - 1)
        edat(1,z) = sigeff(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("109 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding axial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = eps(z,2)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("101 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding hoop strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = eps(z,1)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("102 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding radial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = eps(z,3)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("103 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding elastic hoop strain.
    !
     DO z = 1, (na - 1)
        edat(1,z) = eps(z,1) - epp(z,1) - ThermalStrain(z,1)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("136 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding elastic axial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = eps(z,2) - epp(z,2) - ThermalStrain(z,2)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("137 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding elastic radial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = eps(z,3) - epp(z,3) - ThermalStrain(z,3)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("138 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding permanent axial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = epp(z,2)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("104 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding permanent hoop strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = epp(z,1)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("105 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding permanent radial strain.
    !
    DO z = 1, (na - 1)
        edat(1,z) = epp(z,3)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("106 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the cladding creep rate.
    !
    DO z = 1, (na - 1)
        edat(1,z) = creapratearray(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("144 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Axial Strain due to Irradiation growth (%) (Scanair output).
    !
    DO z = 1, (na - 1) 
        edat(1,z) = CladIrradGrowStrn(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("152 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the clad inner radius permanent surface displacement
    !
    DO z = 1, (na - 1)
        edat(1,z) = totinner(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("169 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the clad outer radius permanent surface displacement
    !
    DO z = 1, (na - 1)
        edat(1,z) = totcrl(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("170 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Fuel Surface Outward Displacement.
    !
    DO z = 1, (na - 1)
        edat(1,z) = totdef(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("128 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the fuel thermal expansion
    !
    DO z = 1, (na - 1)
        edat(1,z) = fuelexptot(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("161 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the fuel swelling
    !
    DO z = 1, (na - 1)
        edat(1,z) = fuelswltot(z) - fuelcreeptot(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("162 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the fuel densification
    !
    DO z = 1, (na - 1)
        edat(1,z) = fueldentot(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("164 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the fuel relocation (before any recovery)
    !
    DO z = 1, (na - 1) 
        edat(1,z) = Relocation(z) * 1000.0_r8k / 2.0_r8k
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("165 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the fuel swelling rate.
    !
    DO z = 1, (na - 1)
        edat(1,z) = dpwxrate(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("143 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Pellet Surface Axial Strain
    !
    DO z = 1, (na - 1)
        edat(1,z) = feps(z,2)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("133 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the nodal FGR.
    !
    DO z = 1, (na - 1)
        SELECT CASE (ngasmod)
        CASE (1, 2, 3)
            edat(1,z) = rdotwrt(z) * 100.0_r8k
        CASE (4)
            edat(1,z) = SUM(RB_axial(:,z)) * 100.0_r8k
        END SELECT
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("141 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the remaining B-10
    !
    IF (ifba > 0.0_r8k .AND. b10 > 0.0_r8k) THEN
        DO z = 1, (na - 1)
            edat(1,z) = boron10(z)
            IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
        END DO
        WRITE (punit,'("148 ",150(e13.5))') (edat(1,z),z=1,na-1)
    END IF
    !
    !***** Output the Structural Gap Interface Pressure.
    !
    DO z = 1, (na - 1)
        edat(1,z) = RinterfacPress(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("131 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Gap Pressure.
    !
    DO z = 1, (na - 1)
        edat(1,z) = GapPress(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("129 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Mechanical Gap.
    !
    DO z = 1, (na - 1)
        edat(1,z) = FuelCladGap(z) * 1000.0_r8k
        IF (.NOT. unit) edat(1,z) = edat(1,z) * miltoum
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("155 ",100(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Thermal Gap.
    !
    DO z = 1, (na - 1)
        edat(1,z) = gapplot(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * miltoum
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("112 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Gap Conductance.
    !
    DO z = 1, (na - 1)
        edat(1,z) = GapCond(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * Bhft2FtoWm2K
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("146 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Outer Oxide Thickness.
    !
    DO z = 1, (na - 1)
        edat(1,z) = EOSZrO2Thk(z) * fttomil
        IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("115 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output Clad Hydrogen Content.
    !
    DO z = 1, (na - 1)
        edat(1,z) = CladH2Concen(z)
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("140 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Coolant Density.
    !
    DO z = 1, (na - 1)
        edat(1,z) = rhof(z)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * lbft3tokgm3
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("117 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Coolant Mass Flux.
    !
    DO z = 1, (na - 1)
        edat(1,z) = go(it)
        IF (.NOT. unit) edat(1,z) = edat(1,z) * lbhrft2toksm2
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("118 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !***** Output the Coolant Pressure.
    !
    DO z = 1, (na - 1)
        IF (ifixedcoolp == 1) THEN
            edat(1,z) = coolantpressure(it,z)
        ELSE
            edat(1,z) = p2(it)
        END IF
        IF (.NOT. unit) edat(1,z) = edat(1,z) * PSItoMPa
        IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
    END DO
    WRITE (punit,'("119 ",150(e13.5))') (edat(1,z),z=1,na-1)
    !
    !**** Output the rim thickness.
    !
    IF (nplot > 1 .AND. ngasmod == 3) THEN
        DO z = 1, (na - 1)
            edat(1,z) = FGRData%FRAPFGR%rimthick(z)
            IF (.NOT. unit) edat(1,z) = edat(1,z) * intomm
            IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(1,z) = 0.0_r8k
        END DO
        WRITE (punit,'("180 ",150(e13.5))') (edat(1,z),z=1,na-1)
    ENDIF
    !
    !***** 3D data
    !
    !***** Fuel Rod Mesh Temperatures
    !
    DO l = 1, (nr + 3)
        DO z = 1, (na - 1)
            SELECT CASE (l)
            CASE (1)
                !***** Output Cladding Oxide Surface Temperatures
                edat(1,z) = SurfTempOxide(z)
            CASE (2)
                !***** Output Cladding Outside Temperatures
                edat(2,z) = CladOutSurfTemp(z)
            CASE (3)
                !***** Output Cladding Inside Temperatures
                edat(3,z) = CladInSurfTemp(z)
            CASE DEFAULT
                !***** Output Fuel Temperatures
                edat(l,z) = tmpfuel(l-3,z)
            END SELECT
            IF (.NOT. unit) edat(l,z) = tfk(edat(l,z))
            IF (z < (jmin - 1) .OR. z > (jmax - 1)) edat(l,z) = 0.0_r8k
        END DO
        WRITE (punit,'("201 ",150(e13.5))') (edat(l,z),z=1,na-1)
    END DO
    !
    IF (nplot > 1) THEN
        !
        !***** Fuel Rod Mesh Coordinates
        !
        !***** Cladding/Oxide Node Positions
        !
        DO z = 1, (na - 1)
            SELECT CASE (mechan)
            CASE (2) ! fracas-I is used
                eps3 = eps(z,3) * 100.0_r8k
            CASE DEFAULT
                eps3 = 0.0_r8k
            END SELECT
            ! Compute inner radius's cladding (Inch)
            rcih = CladDiamHot(z) / 2.0_r8k
            edat(3,z) = rcih
            ! Compute outer radius's cladding (tcc is cladding thickness)
            rcoh = rcih + tcc * (1.0_r8k + eps3 / 100.0_r8k)
            edat(2,z) = rcoh
            ! Compute oxide thickness. Convert from feet to inch
            rox = rcoh + EOSZrO2Thk(z) * fttoin
            edat(1,z) = rox
        END DO
        !
        !***** Fuel Node Positions
        !
        DO l = 1, nr
            DO z = 1, (na - 1) 
                edat(l+3,z) = hrad(l,z)
            END DO
        END DO
        IF (.NOT. unit) THEN
            DO l = 1, (nr + 3)
                DO z = 1, (na - 1)
                    edat(l,z) = edat(l,z) * intomm
                END DO
            END DO
        END IF
        DO l = 1, (nr + 3)
            WRITE (punit,'("202 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
        !
        !***** Fuel Rod Local BurnUp
        !
        edat = 0.0_r8k
        DO l = 1, nr
            DO z = 1, (na - 1)
                edat(l+3,z) = brnup3(z,nr-l+1,2) / 1000.0_r8k
            END DO
        END DO
        DO l = 1, (nr + 3)
            WRITE (punit,'("203 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
        !
        !***** Linear Porosity Fuel Fraction
        !
        edat = 0.0_r8k
        DO l = 1, nr
            DO z = 1, (na - 1) 
                edat(l+3,z) = 3.0_r8k * porosnew(nr-l+1,z)
            END DO
        END DO
        DO l = 1, (nr + 3)
            WRITE (punit,'("204 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
        !
        !***** Pu Concentration
        !
        edat = 0.0_r8k
        DO l = 1, nr
            DO z = 1, (na - 1) 
                ! Calculate number of atoms of Pu / cm3
                punba = conPu239(z,l,2) + conPu240(z,l,2) + conPu241(z,l,2) + conPu242(z,l,2)
                ! Calculate number of atoms of U / cm3
                unba = conU238(z,l,2) + conU235(z,l,2)
                ! Calculate Pu concentration
                edat(nr-l+1+3,z) = punba / (punba + unba)
            END DO
        END DO
        !
        DO l = 1, (nr + 3)
            WRITE (punit,'("205 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
        !
        !***** Radial Profile
        !
        edat = 0.0_r8k
        DO l = 1, nr
            DO z = 1, (na - 1) 
                edat(l+3,z) = rapow(l,z)
            END DO
        END DO
        DO l = 1, (nr + 3)
            WRITE (punit,'("206 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
        !
        !***** Radial Fuel Density
        !
        edat = 0.0_r8k
        DO z = 1, (na - 1)
            DO l = 1, (nr - 1)
                xrhof = 10.96_r8k * (1.0_r8k - 3.0_r8k * porosnew(l,z))
                IF (comp(z) > 0.0_r8k) xrhof = (1.0_r8k - comp(z) / 100.0_r8k) * xrhof + &
                  &                             comp(z) / 100.0_r8k * xrhof * 11.46_r8k / 10.96_r8k
                edat(nr+3-l+1,z) = xrhof
            END DO
        END DO
        DO l = 1, (nr + 3)
            WRITE (punit,'("207 ",100(e13.5))') (edat(l,z),z=1,na-1)
        END DO
    END IF
    !
    !***** 3D data Radial Gas
    !***** Gas Radius
    !
    IF ((nplot > 1) .AND. ((ngasmod == 2) .OR. (ngasmod == 3) .OR. (ngasmod == 4))) THEN
        DO z = 1, (na - 1)
            DO l = 1, nr
                hsfrad(l,z) = hrad(nr-l+1,z)
                csfrad(l) = crad(nr-l+1,z)
                !
                IF (csfrad(l) == 0.0_r8k) THEN
                    epsi(l,z) = 0.0_r8k
                ELSE
                    epsi(l,z) = (hsfrad(l,z) - csfrad(l)) / csfrad(l)
                END IF
            END DO
        END DO
        DO z = 1, (na - 1)
            DO lg = 1, (ngasr + 1)
                rgas = FGRData%SCAN%rdGas(lg,z)
                DO l = 1, (nr - 1)
                    radin = hsfrad(l,z)
                    radou = hsfrad(l+1,z)
                    !
                    IF (rgas >= radin .AND. rgas <= radou) THEN
                        oeps = (epsi(l,z) * (rgas - radin) + epsi(l+1,z) * (radou - rgas)) / (radou - radin)
                        gasrad(lg,z) = (1.0_r8k + oeps) * rgas
                        EXIT
                    END IF
                END DO
            END DO
        END DO
        DO lg = 1, ngasr
            DO z = 1, (na - 1)
                edat(lg,z) = gasrad(lg+1,z)
                IF (.NOT. unit) edat(lg,z) = edat(lg,z) * intomm
            END DO
        END DO
        DO lg = ngasr, 1, -1
            WRITE (punit,'("501 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Calculate Gas Concentrations
        !
        !***** Concentration Of Produced Fission Gas
        !
        DO z = 1, (na - 1)
            xrhouo2(z) = (den - deng) * 10.96_r8k * 0.01_r8k
            IF (comp(z) > 0.0_r8k) xrhouo2(z) = (1.0_r8k - comp(z) / 100.0_r8k) * xrhouo2(z) + &
              &                                  comp(z) / 100.0_r8k * xrhouo2(z) * 11.46_r8k / 10.96_r8k
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%proGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("502 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Concentration Of Released Fission Gas
        !
        DO z = 1, (na - 1) 
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%relGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("503 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Concentration Of Retained Fission Gas
        !
        DO z = 1, (na - 1) 
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%conGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("504 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Concentration Of Gas at Grain Boundaries (gb - Intergranular gas) moles/m^3
        !
        DO z = 1, (na - 1) 
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%interGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("505 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Concentration of Gas Within Grains (gg - Intragranular gas) moles/m^3
        !
        DO z = 1, (na - 1)
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%intraGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("506 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Concentration of Re-Solved Gas (grs - Re-Solved gas) moles/m^3
        !
        DO z = 1, (na - 1)
            DO lg = 1, ngasr
                edat(lg,z) = (22.4_r8k * 1.0e-3_r8k * FGRData%SCAN%resolGas(lg,z)) / xrhouo2(z)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("507 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Fuel restructuring
        !
        DO z = 1, (na - 1)
            DO lg = 1, ngasr
                edat(lg,z) = FGRData%FRAPFGR%restructure(z,lg)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("520 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
        !
        !***** Fuel porosity
        !
        DO z = 1, (na - 1)
            DO lg = 1, ngasr
                edat(lg,z) = FGRData%FRAPFGR%porosity(z,lg)
            END DO
        END DO
        DO lg = 1, ngasr
            WRITE (punit,'("521 ",100(e13.5))') (edat(lg,z),z=1,na-1)
        END DO
    END IF
    !
    IF (ALLOCATED(edat)) DEALLOCATE(edat)
    !
    END SUBROUTINE grafout
    !
END MODULE PlotFile

