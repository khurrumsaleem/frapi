MODULE HeatCond
    USE Kinds
    IMPLICIT NONE
    !
    CONTAINS
    !
    SUBROUTINE qcon (a1, indxjk, RadialBound, dtheta, nmesh, igpnod, ncladi, naxn, naz, nzmesh, kt, &
      &              kz, ndim, nsymm, IterationCount, nnaz, kthta, k, j, HeatFlow, ntstep, tfmelt, &
      &              rhof, TimeIncrement, nvoids, voidcp, fotmtl, frden, qcold, qctot, gadolin, burad)
    USE Kinds
    USE conversions_fraptran, ONLY : pi, tfk, tkf
    USE variables_fraptran, ONLY : ounit, Time, ndebug
    USE phypro_h
    USE Material_Properties, ONLY : MatProperty
    IMPLICIT NONE
    !>@brief
    !> Subroutine computes heat conducted in azimuthal and axial directions into row of meshes on a radial line
    !
    ! Input/Output
    !
    ! a1             - array used for storage in vector form
    ! indxjk         - vector storing starting indexes in array a1 of quantities needed in heat conduction calculations.
    !                  See subroutine starti for element of indxjk that stores starting index of each needed quantity
    ! RadialBound(l) - initial cold state radial coordinate of radial node l (ft)
    ! dtheta         - angular sweep of each azimuthal mesh interval (degree)
    ! AxialNodLen(k) - length associated with k-th axial node  (ft)
    ! nmesh          - number of radial nodes
    ! igpnod         - radial node number at fuel surface
    ! ncladi         - radial node number at inside surface of cladding
    ! naxn           - number of axial nodes
    ! naz            - number of azimuthal nodes
    ! nzmesh         - number of axial nodes in refined r-z mesh
    ! kt             - azimuthal node number of radial row of meshes for which azimuthal heat conduction is to be computed
    ! kz             - axial node in refined r-z mesh at which axial heat conduction is to be computed
    ! ndim           - mesh indicator. 1 = r-theta,2 = r-z, 3 = r-theta-z
    ! HeatFlow(l)    - heat conducted from azimuthal and axial directions into l-th radial mesh  (btu/ft**3-sec)
    !                  Computed for mid-point of time step. 
    !                  Computed for volume occupied by half-meshes bordering radial node l on inner and outer sides
    ! nsymm          - symmetry indicator. 0 = quarter symmetry, 1 = half 2 = no symmetry
    ! IterationCount - iteration number in overall temperature-deformation-pressure loop
    ! ntstep         - time step number. If ntstep = 1, always steady-state calculation
    ! tfmelt         - fuel melting temperature (F)
    ! rhof           - fuel density (lbm/ft**3)
    ! TimeIncrement  - time step (sec)
    ! nvoids         - Flag to specify if central void in fuel. 0 = no central void in fuel , = 1 = yes
    ! coidcp         - volumetric heat capacity of central void (btu/ft3-F)
    ! nnaz           - number of axial nodes that have two or more azimuthal nodes
    ! kthta          - number of axial nodes between 1 and k that have 2 or more azimuthal nodes
    ! k              - current axial node number
    !
    INTEGER(ipk), INTENT(IN) :: naxn
    INTEGER(ipk) :: iterationcount, kt, k, l, nmesh, is0, is1, nthta, naz, nnaz, nzmesh, &
      &             ndim, j, kthta, ngen, ngen2, kz, ntstep, nvoids, ncladi, igpnod, nsymm, ntmx
    REAL(r8k) :: qctot, tsump, rsump, tsum, rsum, tz1, twht, tckt, tp1, tcktp, tp2, bulocal, frden, &
      &          fotmtl, cz1, clnm1p, cz2, cp1, cp2, tcavep, clnz, clnp, clnp1z, clnp1p, clnm1z, &
      &          tcave, frmelt, tfmelt, fcp1, qmult, tz2, rhof, timeincrement, &
      &          area, dtheta, voidcp, dr, rr, tlnz, tlnp, tlnp1z, tlnp1p, tlnm1z, tlnm1p, &
      &          timec, flux, coldw, qz, qp
    REAL(r8k), PARAMETER :: condcorr = 1.6056e-04_r8k
    INTEGER(ipk), DIMENSION(:) :: indxjk
    REAL(r8k), DIMENSION(:) :: a1, RadialBound, HeatFlow, gadolin
    REAL(r8k), DIMENSION(:,:) :: qcold, burad
    !
    IterationCount = 1
    IF (kt == 1) qctot = 0.0_r8k
    IF (ndebug) WRITE(ounit,947) Time, k, kt, IterationCount
947 FORMAT(' QCON: Time = ',e11.4,' k = ',i3,' kt = ',i4,' IterationCount = ',i5)
    !
    DO l = 1, nmesh
        IF (l == 1) THEN
            ! Compute rate at which heat conducted into center half-mesh. Heat conducted radially through surface 
            ! generated by sweeping point with radius equal to 0.5*(r2+r1) (radial coordinates of radial nodes 1 & 2)
            ! through 360 minus azimuthal interval of azimuthal node kt
            ! is0 = start of temp0 array, is1 = start of tempz array
            is0 = indxjk(10)
            is1 = indxjk(9)
            tsump = 0.0_r8k
            rsump = 0.0_r8k
            tsum  = 0.0_r8k
            rsum  = 0.0_r8k
            ! 
            DO nthta = 1, naz
                ! Find 1,nthta generalized index
                CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, 1, k, j, kthta, nthta, ngen, ngen2, kz)
                tz1 = a1(is0+ngen-1)
                twht = 1.0_r8k
                IF (nthta == 1 .OR. nthta == naz) twht = 0.5_r8k
                tsum = tsum + twht * tz1
                rsum = rsum + twht
                IF (nthta == kt) tckt = tz1
                IF (IterationCount > 1) THEN
                      tp1 = a1(is1+ngen-1)
                      twht = 1.0_r8k
                      IF (nthta == 1 .OR. nthta == naz) twht = 0.5_r8k
                      tsump = tsump + twht * tp1
                      rsump = rsump + twht
                      IF (nthta == kt) tcktp = tp1
                      IF (ntstep <= 1) tz1 = tp1
                ELSE
                    tp1 = tz1
                END IF
                ! find 2,nthta generalized index
                CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, 2, k, j, kthta, nthta, ngen, ngen2, kz)
                tz2 = a1(is0+ngen-1)
                IF (IterationCount > 1) THEN
                    tp2 = a1(is1+ngen-1)
                    IF (ntstep <= 1) tz2 = tp2
                ELSE
                    tp2 = tz2
                END IF
                ! Compute fuel thermal conductivities
                cz1 = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tz1), Burnup=burad(k,l), &
                  &                OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                cz2 = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tz2), Burnup=burad(k,l), &
                  &                OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                cp1 = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tp1), Burnup=burad(k,l), &
                  &                OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                cp2 = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tp2), Burnup=burad(k,l), &
                  &                OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                !
                IF (ndebug) WRITE(ounit,955) nthta, tz1
955             FORMAT(' for sector #',i5,' tz1 = ',e17.10)
            ENDDO
            ! 
            IF (IterationCount /= 1) tcavep = tsump / rsump
            ! compute average fuel temperature at center node
            tcave = tsum / rsum
            ! compute power needed to be added to center node of k-th azimuthal
            ! sector during current time step to make centerline temperature
            ! same for all azimuthal sectors. fcp1 has units btu/lbm-f
            IF (nvoids == 1) THEN
                HeatFlow(l) = voidcp * (tcave - tckt) / TimeIncrement
            ELSE
                frmelt = 0.0_r8k
                IF (tcave > tfmelt) frmelt = 1.0_r8k
                ! Fuel specific heat
                fcp1 = MatProperty (Material='FUEL', Property='SPECHEAT', Temperature=tfk(tcave), Fraction_TD=frden, &
                  &                 Pu=compmt, OMRatio=fotmtl, Fraction_Melt=frmelt) / 4186.8_r8k
                !
                qmult = 1.0_r8k
                HeatFlow(l) = qmult * rhof * fcp1 * (tcave - tckt) / TimeIncrement
                IF (ndebug) THEN
                    WRITE(ounit,965) rhof,fcp1,TimeIncrement
965                 FORMAT(' rhof = ',e11.4,' fcp1 = ',e11.4,' TimeIncrement = ',e11.4)
                    WRITE(ounit,953) Time,kt,tcave,tckt,HeatFlow(l)
953                 FORMAT(' Time = ',e11.4,' kt = ',i4,' tcave = ',e11.4,' tckt = ',e11.4,' qc = ',e11.4)
                END IF
                IF (IterationCount /= 1) HeatFlow(l) = 1.0_r8k * rhof * fcp1 * (tcavep - tcktp) / TimeIncrement + qcold(k,kt)
                qcold(k,kt) = HeatFlow(l)
                IF (ndebug) WRITE(ounit,957) IterationCount, qcold(k,kt)
957             FORMAT(' IterationCount = ',i5,' qcold = ',e11.4)
                area = (dtheta / 360.0_r8k) * pi * (0.5_r8k * (RadialBound(l) + RadialBound(l+1))) ** 2
                qctot = qctot + HeatFlow(l) * area
            END IF
        ELSE IF (l == ncladi) THEN
            ! Compute rate at which heat conducted into half mesh forming inner ring of cladding
            dr = 0.5_r8k * (RadialBound(l+1) - RadialBound(l))
            rr = RadialBound(l) + 0.25_r8k * (RadialBound(l+1) - RadialBound(l))
            ! Compute cross-sectional area of mesh
            area = (dtheta / 360.0_r8k) * pi * ((0.5_r8k * (RadialBound(l+1) + RadialBound(l))) ** 2 - RadialBound(l) ** 2)
            ! tlnz  = temperature at radial node l, azimuthal node n at start of time step
            ! tlnp  = temperature at End of time step
            ! m1    = denotes ' minus 1'
            ! p1    = denotes ' plus 1 '
        ELSE IF (l == nmesh .OR. l == igpnod) THEN
            dr = RadialBound(l) - 0.5_r8k * (RadialBound(l) + RadialBound(l-1))
            rr = RadialBound(l) - 0.25_r8k * (RadialBound(l) - RadialBound(l-1))
            area = (dtheta / 360.0_r8k) * pi * (RadialBound(l) ** 2 - (RadialBound(l) - &
              &     0.5_r8k * (RadialBound(l) - RadialBound(l-1))) ** 2)
        ELSE
            dr = 0.5_r8k * (RadialBound(l+1) - RadialBound(l-1))
            rr = RadialBound(l)
            area = (dtheta / 360.0_r8k) * pi * ((0.5_r8k * (RadialBound(l) + RadialBound(l+1))) ** 2 - &
              &    (0.5_r8k * (RadialBound(l) + RadialBound(l-1))) ** 2)
        END IF
        IF (l > 1) THEN
            ! Find temperatures at all needed points around node under consideration
            ! is0 = starting index of temperatures at start of time step
            ! is1 = starting index of temperatures at End of time step
            ! see Subroutine starti to determine indxjk array indexes for this
            is0 = indxjk(10)
            is1 = indxjk(8)
            ! l,n generalized index computed in Subroutine idxgn1
            CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt, ngen, ngen2, kz)
            tlnz = a1(is0+ngen-1)
            IF (IterationCount <= 1) THEN
                tlnp = tlnz
            ELSE
                tlnp = a1(ngen+is1-1)
                IF (ntstep <= 1) tlnz = tlnp
            END IF
            ! l,n+1 generalized index
            IF (kt == naz) THEN
                ! See if boundary is line of symmetry
                IF (nsymm == 2) THEN
                    ! Azimuthal node 1 is boundary for no symmetry case
                    CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, naz, ngen, ngen2, kz)
                    tlnp1z = a1(is0+ngen-1)
                    IF (IterationCount <= 1) THEN
                        tlnp1p = tlnp1z
                    ELSE
                        tlnp1p = a1(is1+ngen-1)
                        IF (ntstep <= 1) tlnp1z = tlnp1p
                        ! l, n-1 node
                    END IF
                ELSE
                    ! line of symmetry. same temperature at node l,n+1 as at l,n-1
                    CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt-1, ngen, ngen2, kz)
                    tlnp1z = a1(is0+ngen-1)
                    IF (IterationCount <= 1) THEN
                        tlnp1p = tlnp1z
                    ELSE
                        tlnp1p = a1(is1+ngen-1)
                        IF (ntstep <= 1) tlnp1z = tlnp1p
                    END IF
                END IF
            ELSE
                CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt+1, ngen, ngen2, kz)
                ! Force no heat to be transferred from node on line of symmetry
                tlnp1z = a1(is0+ngen-1)
                IF (IterationCount <= 1) THEN
                    tlnp1p = tlnp1z
                ELSE
                    tlnp1p = a1(is1+ngen-1)
                    IF (ntstep <= 1) tlnp1z = tlnp1p
                END IF
            END IF
            !
            IF (kt == 1) THEN
                IF (nsymm == 2) THEN
                    ! no symmetry case. n = naz = boundary azimuthal node
                    ntmx = naz
                    CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, ntmx,ngen, ngen2, kz)
                    tlnm1z = a1(is0+ngen-1)
                    IF (IterationCount <= 1) THEN
                        tlnm1p = tlnm1z
                    ELSE
                        tlnm1p = a1(is1+ngen-1)
                        IF (ntstep <= 1) tlnm1z = tlnm1p
                    END IF
                ELSE
                    ! force no heat transfer across line of symmetry
                    CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt+1, ngen, ngen2, kz)
                    tlnm1z = a1(is0+ngen-1)
                    IF (IterationCount <= 1) THEN
                        tlnm1p = tlnm1z
                    ELSE
                        tlnm1p = a1(is1+ngen-1)
                        IF (ntstep <= 1) tlnm1z = tlnm1p
                    END IF
                END IF
            ELSE
                CALL idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt-1,ngen, ngen2, kz)
                tlnm1z = a1(is0+ngen-1)
                IF (IterationCount <= 1) THEN
                    tlnm1p = tlnm1z
                ELSE
                    tlnm1p = a1(is1+ngen-1)
                    IF (ntstep <= 1) tlnm1z = tlnm1p
                END IF
            END IF
            !
            IF (ndebug) THEN
                WRITE(ounit,901) k, kt, l, IterationCount, tlnp1p, tlnp, tlnm1p
901             FORMAT(' k = ',i3,' kt = ',i3,' l = ',i3,' IterationCount = ',i3,' tlnp1p = ',e13.6, &
                  &    ' tlnp = ',e13.6,' tlnm1p = ',e13.6)
                WRITE(ounit,903) tlnp1z, tlnz, tlnm1z
903             FORMAT(' tlnp1z = ',e13.6,' tlnz = ',e13.6,' tlnm1z = ',e13.6)
            END IF
            ! compute thermal conductivity values at each needed node. check to see if node overlaps fuel, gas gap, or cladding
            IF (l <= igpnod) THEN
                
                ! Fuel region. clnz = conductivity at radial node l, azimuthal node n at start of time step
                clnz   = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnz), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                clnp   = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnp), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                clnp1z = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnp1z), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                clnp1p = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnp1p), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                clnm1z = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnm1z), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
                clnm1p = MatProperty (Material='FUEL', Property='THERMCOND', Temperature=tkf(tlnm1p), Burnup=burad(k,l), &
                  &                   OMRatio=fotmtl, Fraction_TD=frden, Gadolinia=gadolin(k), Pu=compmt) * condcorr
            ELSE IF (l < ncladi) THEN
                
                ! Gap
                clnz = 0.0_r8k
                clnp = 0.0_r8k
                clnp1z = 0.0_r8k
                clnp1p = 0.0_r8k
                clnm1z = 0.0_r8k
                clnm1p = 0.0_r8k
            ELSE
                
                ! Cladding region
                clnz   = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnz), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
                clnp   = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnp), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
                clnp1z = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnp1z), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
                clnp1p = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnp1p), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
                clnm1z = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnm1z), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
                clnm1p = MatProperty (Material='CLAD', Property='THERMCOND', Temperature=tkf(tlnm1p), &
                  &                   Flux=0.0_r8k, ColdWork=0.0_r8k) * condcorr
            ENDIF
            
            ! Debugging
            IF (ndebug) THEN
                WRITE(ounit,941) l, tlnz, clnz, tlnm1p, clnm1p
941             FORMAT(' at l = ',i3,' t = ',e11.4,' k = ',e11.4,' at t = ',e11.4,' k = ',e11.4)
                WRITE(ounit,945) dr, area, rr, dtheta
945             FORMAT(' dr = ',e13.6,' area = ',e13.6,' rr = ',e13.6,' dtheta = '  ,e13.6)
            END IF
            ! Compute rate at which heat conducted into radial node l, azimuthal node n from azimuthal direction  (btu/ft**3-sec)
            qz = (0.5_r8k * dr / (area * rr * dtheta * pi / 180.0_r8k)) * ((clnp1z + clnz) * (tlnp1z - tlnz) + &
              &  (clnz + clnm1z) * (tlnm1z - tlnz))
            qp = (0.5_r8k * dr / (area * rr * dtheta * pi / 180.0_r8k)) * ((clnp1p + clnp) * (tlnp1p - tlnp) + &
              &  (clnp + clnm1p) * (tlnm1p - tlnp))
            HeatFlow(l) = qz
            qctot = qctot + HeatFlow(l) * area
            !
            IF (ndebug) THEN
                WRITE(ounit,959) qz, tlnp1z, tlnz, tlnm1z
959             FORMAT(' qz = ',e15.8,' tlnp1z = ',e15.8,' tlnz = ',e15.8,' tlnm1z = ',e15.8)
                WRITE(ounit,961) qp, tlnp1p, tlnp, tlnm1p
961             FORMAT(' qp = ',e15.8,' tlnp1p = ',e15.8,' tlnp = ',e15.8,' tlnm1p = ',e15.8)
                WRITE(ounit,963) clnp1z, clnz, clnm1z, clnp1p, clnp, clnm1p
963             FORMAT(' clnp1z = ',e11.4,' clnz = ',e11.4,' clnm1z = ',e11.4,' clnp1p = ',e11.4, &
                  &    ' clnp = ',e11.4,' clnm1p = ',e11.4)
                WRITE(ounit,943) l, HeatFlow(l)
943             FORMAT(' at l  = ',i2,' q = ',e13.6)
            ENDIF
            ! If azimuthal node on line of symmetry, assume no azimuthal heat transfer
        ENDIF
    ENDDO
    !
    IF (ndebug) THEN
        WRITE(ounit,949)
949     FORMAT(' qc vector ')
        WRITE(ounit,951) (HeatFlow(l),l = 1,nmesh)
951     FORMAT(8(2x,e11.4))
    ENDIF
    IF (kt == naz .AND. ndebug) WRITE(ounit,967) qctot, Time
    ! qctot = error in energy balance induced by azimuthal heat conduction (btu/s-ft)
967 FORMAT(//' qctot = ',e13.6,' Time = ',e13.6)
    !
    END SUBROUTINE qcon
    !
    !
    !
    SUBROUTINE idxgn1 (nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt, ngen, ngen2, kz)
    USE Kinds
    IMPLICIT NONE
    !>@brief
    !> Subroutine computes index for vector storage form of multi-Dimensional arrays with running index 
    !> for radial, axial, and azimuthal nodes
    !
    ! Input
    !
    ! k      - axial node number
    ! l      - radial node number
    ! j      - fuel rod number
    ! kz     - refined r-z mesh node number
    ! kt     - azimuthal sector number
    ! naxn   - number of axial nodes
    ! nmesh  - number of radial nodes
    ! nnaz   - number of axial nodes with multi-azimuthal sectors
    ! naz    - total number of azimuthal sectors
    ! nzmesh - number of axial nodes in refined r-z mesh
    ! kthta  - number of axial nodes passed by in current axial sweep with azimuthal sectors
    !
    ! Output
    !
    ! ngen   - index for vector storage in array a1 of radial node quantities
    ! ngen2  - index for vector storage of half-mesh quantities (energy absorbed in melting in particular)
    !
    INTEGER(ipk) :: nmsh2
    INTEGER(ipk), INTENT(IN) :: nmesh, naxn, nnaz, naz, nzmesh, ndim, l, k, j, kthta, kt, kz
    INTEGER(ipk), INTENT(OUT) :: ngen, ngen2
    !
    nmsh2 = 2 * nmesh
    !
    ngen  = (j - 1) * nnaz * naz * nmesh + (kthta - 1) * naz * nmesh + (kt - 1) * nmesh + l
    ngen2 = (j - 1) * nnaz * naz * nmsh2 + (kthta - 1) * naz * nmsh2 + (kt - 1) * nmsh2 + 2 * (l - 1) + 1
    !
    END SUBROUTINE idxgn1
!
END MODULE HeatCond

